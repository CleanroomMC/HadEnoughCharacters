buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "${mc_version}-${proj_version}"
group = "me.towdium.jecharacters"
archivesBaseName = "jecharacters"

//noinspection GroovyAssignabilityCheck
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2838"
    runDir = "run"
    mappings = "stable_nodoc_39"

    replace '@VERSION@', project.version
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    deobfCompile 'curse.maven:had-enough-items-557549:3831825'
    shade files("libs/PinIn-1.5.0-all.jar")

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.2.0'
}


repositories {
    mavenCentral()
    maven { url = "https://dvs1.progwml6.com/files/maven" }
    maven { url = "modmaven.k-4u.nl" }
    maven { url "https://cursemaven.com" }
    maven { url 'https://jitpack.io' }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes 'FMLCorePlugin': 'me.towdium.hecharacters.core.HechCore'
    }
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            include 'me/towdium/pinin/**'
        }
    }
}
